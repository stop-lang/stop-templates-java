plugins {
    id 'java'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

group 'org.stop_lang'
version '1.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/stop-lang/stop"
    }
}

dependencies {
    if (rootProject.hasProperty("development")) {
        implementation project(":stop")
    }else {
        implementation 'org.stop_lang:stop:1.1.0'
    }

    implementation group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'
    implementation group: 'org.antlr', name: 'symtab', version: '1.0.8'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'stop'
        name = 'stop-templates'
        userOrg = 'stop-lang'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/stop-lang/stop-runtime-java.git'
        version {
            name = version
            desc = version
            released  = new Date()
            vcsTag = version
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    publish = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'stop-templates'
            groupId = group
            version version
            from components.java
            artifact sourcesJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Stop Templates'
                description = 'Stop Templates is a language for defining software systems.'
                url = 'https://www.stop-lang.org'
                licenses {
                    license {
                        name = 'MIT LICENSE'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
            }
        }
    }
}