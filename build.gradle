plugins {
    id 'java'
    id 'maven-publish'
}

group 'org.stop_lang'
version '1.2.0'

sourceCompatibility = 1.8

repositories {
    maven {
        url  "https://stop-maven.s3.amazonaws.com"
    }
    mavenCentral()
}

dependencies {
    if (rootProject.hasProperty("development")) {
        implementation project(":stop")
    }else {
        implementation 'org.stop_lang:stop:1.2.0'
    }

    implementation group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'
    implementation group: 'org.antlr', name: 'symtab', version: '1.0.8'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    repositories {
        maven {
            url "s3://stop-maven"
            credentials(AwsCredentials) {
                accessKey "${System.getenv('AWS_KEY')}"
                secretKey "${System.getenv('AWS_SECRET')}"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'stop-templates'
            groupId = group
            version version
            from components.java
            artifact sourcesJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Stop Templates'
                description = 'Stop Templates is a language for defining software systems.'
                url = 'https://www.stop-lang.org'
                licenses {
                    license {
                        name = 'MIT LICENSE'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
            }
        }
    }
}